=== Day1_Dover_JV ===
//Day 1: DOVER connects for a private conversation with JV.

//WHERE WE LEFT OFF: Consolidating the conversation into one standard convo that branches out and bottlenecks. 

//TESTING PURPOSES, COMMENT OUT WHEN DONE IN THIS FILE
    // ~ day1_sceneChoice = true 
        //Baha_Ducksly = true
    // ~ day1_sceneChoice = false 
        //Baha_JV = false

Lt Dover "Guess I'll see what's up with J.V. #Dover
//dover patches into J.V
//SFX: connection, line picking up

Officer JV "Dove! You got a second? #JV

Lt Dover "Hmm, let me see... #Dover

Lt Dover "... #Dover

Lt Dover "Yup, looks like I can squeeze you in. #Dover

Officer JV "Yeah? You got jokes? #JV

Officer JV "I'll only need a minute, I promise. Just wanted to vent about something. #JV

Lt Dover "Oh no! What's wrong? #Dover

Officer JV "You already know. #JV

Lt Dover "Do I? #Dover

{day1_sceneChoice == true: //true = Baha & Ducksly spoke
    -> D1_JV_BD
- else: //false = Baha & JV spoke
    -> D1_JV_BJV
}

=== D1_JV_BD === //Day 1: Baha & Ducksly Spoke
Officer JV "Baha. He didn't answer me! #JV

* Oh no! How could he! #Dover
* Captain's probably just a busy guy. #Dover

- Officer JV "All I'm saying is that the Captain should always be on call for his crew. #JV

* There will be other chances to call him Baha Blast. #Dover
    Officer JV "(Groans) You pass up the gig of a lifetime so you can stay on the ship of your favorite captain, the least he could do is answer. #JV 
* He just didn't answer this one time. It's hardly the end of the world. #Dover
    Officer JV "It's not just this "one time" though. #JV 

- -> D1_DJV_Continue1

=== D1_JV_BJV === //Day 1: Baha & JV Spoke
Officer JV "I feel like he still doesn't take me seriously. #JV 

Lt Dover "What do you mean? You're Head of Engineering, I don't think it gets much more serious than that. #Dover

Officer JV "It's not my position on the crew, it's me and my friend. #JV

* You've known each other a long time. #Dover
    Officer JV "Since my first grunt rotation. 'Plucked me from obscurity'. #JV
* Doesn't that muddy things? #Dover
    Officer JV "It hasn't before. 10 years we've known each other. I don't know what's changed. #JV
    
- -> D1_DJV_Continue1

=== D1_DJV_Continue1 ===
Officer JV "Ever since we left on this mission, Baha's been such a hardass. #JV 

Lt Dover "To be honest, I wouldn't want to be in the Captain's shoes. #Dover

Officer JV "(Chuckling) Yeah, you'd look like a clown in them. #JV

Officer JV "I don't know...We used to be really close, but now it's like he hardly wants to talk. Not about the real stuff. #JV 

Lt Dover "I'm sure you guys will find time to chat. #Dover

Officer JV "We do! And when we do it's great! But just getting him to chat is such a struggle now. #JV 

Officer JV "I just want us to go back to being our little squad. #JV 

Lt Dover "Hmm... #Dover

*Sometimes, people drift apart. Might be a sign to let go, move on.
-> D1_JV_DistanceBaha
*You should tell him how you feel.
-> D1_JV_ReconcileBaha

VAR d1JVBahaChoice = ""

=== D1_JV_DistanceBaha ===
~ d1JVBahaChoice = "distance"
Officer JV "You really think so? #JV 

Lt Dover "We're coming up on the Edge, the literal end of everything. Why not? #Dover

Officer JV "I guess I would just feel bad. #JV 

Lt Dover "Don't! It happens. You don't have to be mean, but you don't have to keep trying if he's not. #Dover

Officer JV "I'd miss out on his writing (chuckles). #JV 

Officer JV "But maybe you're right. Thanks, Doverino! #JV 

~ B_JV_relCount -- //reduce B_JV_relCount by 1

->DONE

=== D1_JV_ReconcileBaha ===
~ d1JVBahaChoice = "reconcile"
Officer JV "Share my feelings with Captain Stoicism? That's a big yikes for me. #JV 

Lt Dover "So? #Dover

Officer JV "Did you not just hear me say- #JV

Lt Dover "I know what you said, but we're coming up on the Edge, the literal end of everything. Now's the perfect time to put things right. #Dover

Officer JV "Hmm... #JV

Lt Dover "How about this: What if when we reach the Edge, and all that's there is a blank void inhabited by an incompressible space squid monster. #Dover

Lt Dover "You wanna die on bad terms with the Captain in that scenario? #Dover

Officer JV "You think there's a giant space squid monster? #JV 

Lt Dover "What? No, it was... That's not the point J.V. #Dover

Officer JV "No, you're right. If there is a space squid monster, Baha and I will challenge it head on together. #JV 

Officer JV "Thanks Doves, I appreciate it!

~ B_JV_relCount ++ //reduce B_JV_relCount by 1

// -> Day2_Start
-> DONE