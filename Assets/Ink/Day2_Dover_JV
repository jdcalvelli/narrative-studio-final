=== Day2_Dover_JV ===
//Day 2: Dover connects for a private conversation with JV.

//FOR TESTING PURPOSES, COMMMENT OUT WHEN DONE
// ~ day1_sceneChoice = true 
// ~ day2_sceneChoice = true 

Lt Dover "Let's see what J.V. is up to. #Dover
//dover patches into J.V
//SFX: connection, line picking up

Lt Dover "Hey, J.V. Yeah, I got a minute. Just a minute, though. #Dover

Officer JV "Oh, again with the jokes, I see. #JV

Lt Dover "Not in the mood for jokes? #Dover

Officer JV "Not really, not right now. #JV 

Lt Dover "What's going on? #Dover

{day2_sceneChoice == true: //false = JV & Ducksly spoke
    -> D2_JV_JVD
- else: //false = JV & Baha spoke
    -> D2_JV_JVB
}

=== D2_JV_JVB=== //Day 2, JV spoke to Baha
Officer JV "Baha read me some of his writing. #JV

Lt Dover "No way. #Dover

Officer JV "Way. #JV 

Lt Dover "How was it? 

* Was it sappy? #Dover
    Officer JV "Yeah, it was actually! #JV
    Officer JV "Surprising, I know, but he's always had this soft, gooey, "I'd rather die than causally show a different side of myself" center. #JV
* Was it testosterone, action-packed? #Dover
    Officer JV "You'd think that wouldn't you? No, it was much softer. #JV 

- Lt Dover "Was it any good, though? #Dover

//COMMENT THIS VARIABLE OUT WHEN DONE
// ~ jvReview = "pos"
{jvReview == "pos": //jv gave a positive review to Baha
    <- D2_posReview
- else: //jv gave a negative review to Baha
    <- D2_negReview
}

Officer JV "I don't know, I'm glad he opened up but then he immediately shut down again. #JV 
Officer JV "It's very frustrating and I don't like being yanked away from my work if he's gonna be like that. #JV

Lt Dover "Did you talk to him about this yet? #Dover 

Officer JV "No... #JV

//COMMENT THIS OUT WHEN DONE. 
// ~ d1JVBahaChoice = "reconcile"
// ~ d1JVBahaChoice = "distance"

Lt Dover "J.V., come on. #Dover

{d1JVBahaChoice == "distance": Lt Dover "We talked about this. You were supposed to distance yourself.} #Dover
{d1JVBahaChoice == "reconcile": Lt Dover "We talked about this. You were supposed to set things right.} #Dover

Officer JV "I know, I know. #JV
Officer JV "But feelings are hard. #JV

* His feelings aren't your fault. Ignore him. #Dover 
    -> D2_JV_DistanceBaha
* Maybe you "both" should open up? Like, "actually" this time? #Dover
    -> D2_JV_ReconcileBaha

=== D2_JV_DistanceBaha ===
~ B_JV_relCount -- //Lower Baha & JV rel by 1

Officer JV "You're totally right. #JV
Officer JV "I'M right. #JV

Lt Dover "Sometimes you just need confirmation from an impartial third party. #Dover 

Officer JV "Dover, next time he tries to call me, send him straight to switchboard voicemail. #JV
Officer JV "Unless it's like, an actual emergency. #JV

Lt Dover "You got it, J.V. #Dover 

Officer JV "By Dover, J.V. out. #JV

//the line disconnects

-> D2_Dover_JV_End

=== D2_JV_ReconcileBaha ===
~ B_JV_relCount ++ //raise Baha & JV rel by 1

Officer JV "Lead by example? The emotionally inept leading the emotionally inept. #JV

Lt Dover "I wouldn't say either of you are emotionally inept. #Dover
Lt Dover "I mean, look at us! We're talking, aren't we? #Dover

Officer JV "I guess. Just don't ask me to make meaningful eye contact. #JV

Officer JV "Ok, I'll "actually" take your advice this time. #JV
Officer JV "'The Edge awaits', 'seize the day', all that nonsense. #JV
Officer JV "Bye Dover. #JV

Lt Dover "Bye, J.V. #Dover 

-> D2_Dover_JV_End

/// JV & BAHA relevant THREADS ////////////////////////
=== D2_posReview === 
Officer JV "I'll tell you what I told Baha: I'm no literary critic, but those words sounded pretty to me. #JV

Lt Dover "That's good then, right? #Dover

Officer JV "Yeah, I guess so. #JV

Lt Dover "You don't seem so thrilled? #Dover 

-> DONE

=== D2_negReview === 
Officer JV "I don't know...bad? #JV

Lt Dover "Oh no.. #Dover

Officer JV "Oh yes. I don't think he took it well either. #JV

-> DONE

//////////////////////////////////////////////////////////////////////
=== D2_JV_JVD === //Day 2, JV spoke to Ducksly
Officer JV "I think Ducksly is really mad at me. #JV 

Lt Dover "What makes you say that? #JV

Officer JV "I may have played an itty bitty teeny weeny prank... #JV

Lt Dover "There is nothing ever "itty bitty" or "teeny weeny" about your pranks. #Dover

VAR pastPrankChoice = ""
* Remember the time you microwaved Ducksly's beak oil? #Dover
    ~ pastPrankChoice = "beakoil"
    Officer JV "Hehe, that got messy. But I did make him more, so even stevens? #JV
* Remember the time you jettisoned Ducksly's feather mousse out the airlock? #Dover
    ~ pastPrankChoice = "feathermousse"
    Officer JV "Ha! And he was a fluffy, downy little duckling. Adorable. #JV

- Lt Dover "I still don't think Ducksly appreciated that one either. #Dover 

* Maybe you should lighten up with the pranks? #Dover //improve the JV Ducksly relationship 
    -> D2_JV_ImproveDuckslyRel
* He's a jerk, kinda deserves it if you ask me. Keep the pranks coming. #Dover //worsens the JV Ducksly relationship
    -> D2_JV_WorsenDuckslyRel

=== D2_JV_ImproveDuckslyRel ===
~ JV_Duck_relCount ++ //increase JV Ducksly relationship by 1

Officer JV "Maybe you're right. #JV

Officer JV "Growing up, that's just how we communicated with eachother - being a little mean means I like you. #JV
Officer JV "I guess I misunderstood the dynamic of our relationship. #JV

Lt Dover "There's still time to set things right. 1 more day until the Edge! #JV 

Officer JV "Thanks, Dover. I think I owe Ducksly an apology... #JV
{pastPrankChoice == "beakoil": Officer JV "...and some new beak oil...} #JV
{pastPrankChoice == "feathermousse": Officer JV "...and some new feather mousse...} #JV
Officer JV "...and a repaired centrifuge... #JV
Officer JV "Busy day ahead, then. Bye, Dover! #JV

Lt Dover "Bye, J.V. #Dover 

-> D2_Dover_JV_End

=== D2_JV_WorsenDuckslyRel ===
~ JV_Duck_relCount -- //decrease JV Ducksly relationship by 1

Officer JV "Doverrrrr, that's devious. #JV

Lt Dover "Ducksly has been nothing but rude to you. I think you've been too nice, actually. Ramp it up. #JV

Officer JV "Ya know what? You're right. #JV
Officer JV "What do you think I should do? #JV

//TO DO: I WANNA SWITCH THIS PRANK OUT ACTUALLY, what's the best option?

VAR prankChoice = ""
* Feed him freeze-dried dino nuggets. #Dover
    ~ prankChoice = "dinonuggets"
    Officer JV "That's...dark. #JV
* Steal his pants. #Dover 
    ~ prankChoice = "donaldduck"
    Officer JV "Donald Ducking, classic. #JV
//OPTIONS: Put feather-Nair in his shampoo, feed him dino nuggets

- Officer JV "Thanks, Dover. I'll see what I can get done before we reach the Edge. 
Officer JV "In the meantime, I've got a centrifuge to fix... #JV
{prankChoice == "dinonuggets": Officer JV "...and some nuggets to rehydrate...} #JV
{prankChoice == "donaldduck": Officer JV "...and some duck pants to steal...} #JV
Officer JV "Mwahaha. #JV
Officer JV "Bye, Dover! #JV

Lt Dover "Bye, J.V." #Dover 

-> D2_Dover_JV_End

=== D2_Dover_JV_End ===
// -> Day3_Start
-> DONE